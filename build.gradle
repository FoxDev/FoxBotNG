plugins {
    id 'java'
    id 'application'
    id 'findbugs'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id "info.robotbrain.lombok" version "1.1"
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        def ver = stdout.toString().trim()
        if (System.getenv("BUILD_NUMBER") != null) {
            ver += '-b' + System.getenv("BUILD_NUMBER")
        }
        return ver;
    }
    catch (ignored) {
        return null;
    }
}

group 'co.foxdev.foxbotng'
mainClassName = group + '.' + rootProject.name
version = getVersionName()

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://repo.kitteh.org/content/groups/public/'
    }
}

dependencies {
    compile 'ch.qos.logback:logback-core:1.1.+'
    compile 'ch.qos.logback:logback-classic:1.1.+'
    compile 'com.typesafe:config:1.3.+'
    compile 'org.jsoup:jsoup:1.8.+'
    compile 'org.kitteh.irc:client-lib:0.3.+'
    compile "net.sf.jopt-simple:jopt-simple:4.+"
    compile "org.xerial:sqlite-jdbc:3.7.+"
    compile "org.projectlombok:lombok:1.16.+"
    testCompile 'junit:junit:4.+'
}

shadowJar {
    dependencies {
        exclude(dependency('org.projectlombok:lombok'))
    }
}

jar {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    manifest {
        attributes 'Implementation-Title': mainClassName,
                'Implementation-Version': version,
                'Built-Date': new Date(),
                'Built-By': System.getProperty('user.name'),
                'Build-Host': InetAddress.localHost.canonicalHostName,
                'Built-JDK': System.getProperty('java.version')
    }
}

findbugs {
    ignoreFailures = true
    toolVersion = '3.0.1'
    effort = "max"
    reportLevel = "low"
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/findbugs")
}
