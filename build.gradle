plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        def ver = stdout.toString().trim()
        if (System.getenv("BUILD_NUMBER") != null) {
            ver += '-b' + System.getenv("BUILD_NUMBER")
        }
        return ver;
    }
    catch (ignored) {
        return null;
    }
}

group 'co.foxdev.foxbotng'
def mainClassName = 'co.foxdev.foxbotng.Main'
version = getVersionName()

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://repo.kitteh.org/content/groups/public/'
    }
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.4.1'
    compile 'org.apache.logging.log4j:log4j-core:2.4.1'
    compile 'com.typesafe:config:1.3.0'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'org.kitteh.irc:client-lib:0.3.6'
    compile "org.projectlombok:lombok:1.16.6"
    compile "net.sf.jopt-simple:jopt-simple:4.9"
    compile "org.xerial:sqlite-jdbc:3.7.2"
    testCompile 'junit:junit:4.12'
}

jar {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    manifest {
        attributes 'Main-Class': mainClassName,
                'Implementation-Title': 'Gradle',
                'Implementation-Version': version,
                'Built-Date': new Date(),
                'Built-By': System.getProperty('user.name'),
                'Build-Host': InetAddress.localHost.canonicalHostName,
                'Built-JDK': System.getProperty('java.version')
    }
}